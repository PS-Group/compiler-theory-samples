// All token codes are small integers with #defines that begin with "TK_"
%token_prefix TK_

// The type of the data attached to each token is Token.  This is also the
// default type for non-terminals.
//
%token_type {SToken}
%default_type {SToken}

// The generated parser function takes a 4th argument as follows:
%extra_argument {CBatchParser *pParse}

// This code runs whenever there is a syntax error
//
%syntax_error
{
    (void)yymajor;  // Silence compiler warnings.
    pParse->OnError(TOKEN); // TOKEN has type defined in '%token_type'
}

// This code runs on LALR stack overflow
%stack_overflow
{
    (void)yypMinor; // Silence compiler warnings.
    pParse->OnStackOverflow();
}

// This code runs when parser failed and cannot recover.
%parse_failure
{
    pParse->OnFatalError();
}

// Destructor set just to disable compiler warnings
//
%token_destructor
{
    (void)yypParser;
    (void)yypminor;
    (void)pParse;
}

// The name of the generated procedure that implements the parser
// is as follows:
%name ParseBatchGrammar

// The following text is included near the beginning of the C source
// code file that implements the parser.
//
%include
{
// Generated function: void ParseBatchGrammar(void*, int, SToken, CBatchParser*);

#include "BatchParser_private.h"

using namespace parser_private;

} // end %include

/////////////////////////
////////////////////////
///////////////////////

// An IDENTIFIER can be a generic identifier, or one of several
// keywords.  Any non-standard keyword can also be an identifier.
//
%type expression ExpressionPtr

%destructor expression
{
    delete $$;
    $$ = nullptr;
}

%type if_condition BlockPtr
%destructor if_condition { DestroyBlock(pParse, $$); }

%type if_condition_line BlockPtr
%destructor if_condition_line { DestroyBlock(pParse, $$); }

%type while_condition BlockPtr
%destructor while_condition { DestroyBlock(pParse, $$); }

%type while_condition_line BlockPtr
%destructor while_condition_line { DestroyBlock(pParse, $$); }

%type repeat_head_line BlockPtr
%destructor repeat_head_line { DestroyBlock(pParse, $$); }

%type repeat_head BlockPtr
%destructor repeat_head { DestroyBlock(pParse, $$); }

%type until_condition ExpressionPtr
%destructor until_condition { delete $$; $$ = nullptr; }

%type else_block BlockPtr
%destructor else_block { DestroyBlock(pParse, $$); }

%type else_head_line BlockPtr
%destructor else_head_line { DestroyBlock(pParse, $$); }

%type else_head BlockPtr
%destructor else_head { DestroyBlock(pParse, $$); }

%left PLUS MINUS.
%left STAR SLASH PERCENT.

translation_unit ::= statement_lines_list.

statement_lines_list ::= statement_line.

statement_lines_list ::= statement_lines_list statement_line.

statement_line ::= statement NEWLINE.

statement_line ::= error NEWLINE.

statement ::= ID(A) ASSIGN expression(B).
{
    auto stmt = Make<CAssignAST>(A.stringId, Take(B));
    pParse->AddStatement(std::move(stmt));
}

statement ::= PRINT expression(A).
{
    auto stmt = Make<CPrintAST>(Take(A));
    pParse->AddStatement(std::move(stmt));
}

statement ::= if_condition_line(A) statement_lines_list END.
{
    pParse->ExitBlock();
    pParse->AddStatement(IStatementASTUniquePtr(Take(A)));
}

statement ::= if_condition_line(A) END.
{
    pParse->ExitBlock();
    pParse->AddStatement(IStatementASTUniquePtr(Take(A)));
}

statement ::= if_condition_line(A) statement_lines_list else_block(B).
{
    static_cast<CIfAst*>(A)->SetElseStatement(Take(B));
    pParse->ExitBlock();
    pParse->AddStatement(IStatementASTUniquePtr(Take(A)));
}

else_block(X) ::= else_head_line(A) statement_lines_list END.
{
    X = Take(A).release();
    pParse->ExitBlock();
}

else_head_line(X) ::= else_head(A) NEWLINE.
{
    X = Take(A).release();
}

else_head(X) ::= ELSE.
{
    auto ast = Make<CBlockAst>();
    pParse->EnterBlock(ast.get());
    X = ast.release();
}

statement ::= while_condition_line(A) statement_lines_list END.
{
    pParse->ExitBlock();
    pParse->AddStatement(IStatementASTUniquePtr(Take(A)));
}

statement ::= while_condition_line(A) END.
{
    pParse->ExitBlock();
    pParse->AddStatement(IStatementASTUniquePtr(Take(A)));
}

statement ::= repeat_head_line(A) statement_lines_list until_condition(B) END.
{
    static_cast<CRepeatAst*>(A)->SetCondition(Take(B));
    pParse->ExitBlock();
    pParse->AddStatement(IStatementASTUniquePtr(Take(A)));
}

statement ::= repeat_head_line(A) until_condition(B) END.
{
    static_cast<CRepeatAst*>(A)->SetCondition(Take(B));
    pParse->ExitBlock();
    pParse->AddStatement(IStatementASTUniquePtr(Take(A)));
}

repeat_head_line(X) ::= repeat_head(A) NEWLINE.
{
    X = Take(A).release();
}

repeat_head(X) ::= REPEAT.
{
    auto ast = Make<CRepeatAst>();
    pParse->EnterBlock(ast.get());
    X = ast.release();
}

until_condition(X) ::= UNTIL expression(A).
{
    X = Take(A).release();
}

if_condition_line(X) ::= if_condition(A) NEWLINE.
{
    X = Take(A).release();
}

if_condition(X) ::= IF expression(A).
{
    auto ast = Make<CIfAst>(Take(A));
    pParse->EnterBlock(ast.get());
    X = ast.release();
}

while_condition_line(X) ::= while_condition(A) NEWLINE.
{
    X = Take(A).release();
}

while_condition(X) ::= WHILE expression(A).
{
    auto ast = Make<CWhileAst>(Take(A));
    pParse->EnterBlock(ast.get());
    X = ast.release();
}

expression(X) ::= LPAREN expression(A) RPAREN.
{
    X = Take(A).release();
}

expression(X) ::= expression(A) PLUS expression(B).
{
    X = New<CBinaryExpressionAST>(Take(A), BinaryOperation::Add, Take(B));
}

expression(X) ::= expression(A) MINUS expression(B).
{
    X = New<CBinaryExpressionAST>(Take(A), BinaryOperation::Substract, Take(B));
}

expression(X) ::= expression(A) STAR expression(B).
{
    X = New<CBinaryExpressionAST>(Take(A), BinaryOperation::Multiply, Take(B));
}

expression(X) ::= expression(A) SLASH expression(B).
{
    X = New<CBinaryExpressionAST>(Take(A), BinaryOperation::Divide, Take(B));
}

expression(X) ::= expression(A) PERCENT expression(B).
{
    X = New<CBinaryExpressionAST>(Take(A), BinaryOperation::Modulo, Take(B));
}

expression(X) ::= PLUS expression(A).
{
    X = New<CUnaryExpressionAST>(UnaryOperation::Plus, Take(A));
}

expression(X) ::= MINUS expression(A).
{
    X = New<CUnaryExpressionAST>(UnaryOperation::Minus, Take(A));
}

expression(X) ::= NUMBER(A).
{
    X = New<CLiteralAST>(A.value);
}

expression(X) ::= ID(A).
{
    X = New<CVariableRefAST>(A.stringId);
}
